// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zero
    Not16(in=x, out=notx);
    And16(a=x, b=notx, out=zero);

    // zx
    Mux16(a=x, b=zero, sel=zx, out=x1);

    // nx
    Not16(in=x1, out=nx1);
    Mux16(a=x1, b=nx1, sel=nx, out=x2);

    // zy
    Mux16(a=y, b=zero, sel=zy, out=y1);

    // ny
    Not16(in=y1, out=ny1);
    Mux16(a=y1, b=ny1, sel=ny, out=y2);

    // f
    Add16(a=x2, b=y2, out=xplusy);
    And16(a=x2, b=y2, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=fout);
    
    // no
    Not16(in=fout, out=nfout);
    Mux16(a=fout, b=nfout, sel=no, out=tout);

    // zr
    And16(a=tout, b=tout, out[0..7]=pout1, out[8..15]=pout2);
    Or8Way(in=pout1, out=t1);
    Or8Way(in=pout2, out=t2);
    Or(a=t1, b=t2, out=notzr);
    Not(in=notzr, out=zr);

    // ng
    And16(a=tout, b=tout, out[15]=ng, out=out);
}